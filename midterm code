/*ME*/
#include <NewPing.h>


#define SONAR_NUM 3      // Number of sensors.
#define MAX_DISTANCE 150 // Maximum distance (in cm) to ping.

//벽 간격 거리(밀리미터 단위) 정의
#define WALL_GAP_DISTANCE      400//mm 단위
#define WALL_GAP_DISTANCE_HALF 200 //mm 단위

//모터 PWM 오프셋 값
#define MOTOR_PWM_OFFSET 40

//초음파 센서 핀 정의
#define Front 0
#define Left  1 
#define Right 2

//F
#define TRIG1 14 // 초음파 센서 1번 Trig 핀 번호
#define ECHO1 15 // 초음파 센서 1번 Echo 핀 번호

//R
#define TRIG2 18 // 초음파 센서 2번 Trig 핀 번호
#define ECHO2 19 // 초음파 센서 2번 Echo 핀 번호

//L
#define TRIG3 16 // 초음파 센서 3번 Trig 핀 번호
#define ECHO3 17 // 초음파 센서 3번 Echo 핀 번호

// 초음파 센서 초기화 
NewPing sonar[SONAR_NUM] = {   // Sensor object array.
NewPing(TRIG1, ECHO1, MAX_DISTANCE), // Each sensor's trigger pin, echo pin, and max distance to ping.
NewPing(TRIG2, ECHO2, MAX_DISTANCE),
NewPing(TRIG3, ECHO3, MAX_DISTANCE)
};


/////////////////////L298//////////////////////////
// 모터 제어 핀
#define ENL 6
#define IN1 11
#define IN2 10
#define IN3 9
#define IN4 8
#define ENR 7

//센서 값 변수
 float front_sonar = 0.0;
 float left_sonar  = 0.0;
 float right_sonar = 0.0;

/////////////////////Maze_Status//////////////////////////
//현재 미로 상태 변수
int maze_status = 0; 


void setup() 
{
	
  pinMode(TRIG1, OUTPUT);
  pinMode(ECHO1, INPUT);

  pinMode(TRIG2, OUTPUT);
  pinMode(ECHO2, INPUT);

  pinMode(TRIG3, OUTPUT);
  pinMode(ECHO3, INPUT);

  pinMode(ENL, OUTPUT);
  pinMode(IN1, OUTPUT);
  pinMode(IN2, OUTPUT);

  pinMode(IN3, OUTPUT);
  pinMode(IN4, OUTPUT);
  pinMode(ENR, OUTPUT);
  Serial.begin(9600); // 통신속도를 9600으로 정의함

}

// 모터 A(왼쪽 모터)를 제어하는 함수
void motor_A_control(int direction_a, int motor_speed_a) //모터 A의 방향(direction)과 속도(speed) 제어
{
  if(direction_a == HIGH)
  {
     digitalWrite(IN1, HIGH); //모터의 방향 제어
     digitalWrite(IN2, LOW);
     analogWrite(ENL,motor_speed_a); //모터의 속도 제어

  }
  else
  {
    digitalWrite(IN1, LOW);
    digitalWrite(IN2, HIGH);
    analogWrite(ENL,motor_speed_a);

  }
}
// 모터 B(오른쪽 모터)를 제어하는 함수
void motor_B_control(int direction_b, int motor_speed_b) //모터 A의 방향(direction)과 속도(speed) 제어
{
  if(direction_b == HIGH)
  {
     digitalWrite(IN3, HIGH); //모터의 방향 제어
     digitalWrite(IN4, LOW);
     analogWrite(ENR,motor_speed_b); //모터의 속도 제어

  }
  else
  {
    digitalWrite(IN3, LOW);
    digitalWrite(IN4, HIGH);
    analogWrite(ENR,motor_speed_b);

  }
}


// 센서 값을 기반으로 현재 미로 상태를 확인하는 함수 
void check_maze_status(void)
{
	// 3면이 다 막힌 경우
  if((left_sonar>=0) && (left_sonar<=WALL_GAP_DISTANCE) && (right_sonar>=0) && (right_sonar<=WALL_GAP_DISTANCE) && (front_sonar>=0) && (front_sonar<=WALL_GAP_DISTANCE_HALF))
  {
    maze_status = 4;
    Serial.println("maze_status = 4");
  }
  //직진
  else if( (left_sonar>=0) && (left_sonar<=WALL_GAP_DISTANCE) && (right_sonar>=0) && (right_sonar<=WALL_GAP_DISTANCE) && (front_sonar>=WALL_GAP_DISTANCE_HALF)  )
  {
    maze_status = 1;
    Serial.println("maze_status = 1");
  }
  //오른쪽으로 회전
  else if((left_sonar>=0) && (left_sonar<=WALL_GAP_DISTANCE) && (front_sonar>=0) && (front_sonar<=WALL_GAP_DISTANCE_HALF))
  {
    maze_status = 2;
    Serial.println("maze_status = 2");
  }
  //왼쪽으로 회전
  else if((right_sonar>=0) && (right_sonar<=WALL_GAP_DISTANCE) && (front_sonar>=0) && (front_sonar<=WALL_GAP_DISTANCE_HALF))
  {
    maze_status = 3;
    Serial.println("maze_status = 3");
  }
  else
  {
    maze_status = 0;
    Serial.println("maze_status = 0"); 
  }
}
//먼저 left_sonar = 0; right_sonar = 0; 으로 해서 오른쪽 방향 찾기

// 벽 충돌을 피하고 모터 속도를 조정하는 함수 
void wall_collision_avoid(int base_spped)
{
  float error = 0.0;
  float Kp = 30; //나중에 조정해야 할 값(얼마나 돌지)
  int pwm_control = 0;
  int right_pwm = 0;
  int left_pwm  = 0;
  
  // 좌우 초음파 센서 읽기 값의 차이로 에러 계산
  error = (right_sonar - left_sonar);
  error = Kp * error;  

	// 에러 값을 최대 및 최소 값으로 제한 
  if(error >= 50) error = 50;
  if(error <= -50) error = -50; 

	//에러를 기바능로 PWM 값을 조정
  right_pwm = base_spped - error;
  left_pwm  = base_spped + error;

	// PWM 값이 유효한 범위 내에 있는지 확인
  if(right_pwm <= 0) right_pwm = 0;
  if(left_pwm  <= 0) left_pwm  = 0;
  if(right_pwm >= 150) right_pwm = 150;
  if(left_pwm  >= 70) left_pwm  = 70;

	// 조정된 PWM 값으로 모터를 제어 
  motor_A_control(HIGH,left_pwm); //오른쪽 전진
  motor_B_control(HIGH,right_pwm); // 왼쪽 전진

}
void loop() 
{
  //Serial.print("Duration: ");
  //Serial.println(duration);
  
	// 센서 값을 읽고 밀리미터 단위로 변환
  front_sonar = sonar[Front].ping_cm()*10; // 전방 센서 측정
  left_sonar  = sonar[Left].ping_cm() *10; // 좌축 센서 측정
  right_sonar = sonar[Right].ping_cm()*10; // 우측 센서 측정
  
  // 읽기 값이 없는 경우 센서 값을 최대 거리로 설정
  if(front_sonar == 0.0) front_sonar = MAX_DISTANCE*10; // 0.0 출력이 최대값이므로
  if(left_sonar  == 0.0)  left_sonar = MAX_DISTANCE*10;
  if(right_sonar == 0.0) right_sonar = MAX_DISTANCE*10;

	// 시리얼 모니터에 센서 값을 출력
  Serial.print("L: "); Serial.print(left_sonar); Serial.print(" ");
  Serial.print("F: "); Serial.print(front_sonar); Serial.print(" ");
  Serial.print("R: "); Serial.println(right_sonar);

	// 센서 값을 기반으로 현재 미로 상태 확인
  check_maze_status();

	// 미로 상태에 따른 동작을 수행합니다.
  if(maze_status == 4)
  {
  //정지 한다
  Serial.println("Rotate CCW");
  motor_A_control(HIGH,0);
  motor_B_control(LOW,0);
  delay(1000);


  //180도 회전을 한다
  motor_A_control(HIGH,85); //왼쪽 전진
  motor_B_control(LOW,90); //오른쪽은 후진
  delay(1200);

  //정지 한다
  Serial.println("Rotate CCW");
  motor_A_control(HIGH,0);
  motor_B_control(LOW,0);
  delay(1000);

  //앞으로 간다
  Serial.println("Go Straight");
  motor_A_control(HIGH,200);
  motor_B_control(HIGH,210);

  }

  if(maze_status == 1)
  {
  //앞으로 간다
  Serial.println("run straight");
  wall_collision_avoid(100);
  } 

  else if(maze_status == 3)
  {
  //정지 한다
  Serial.println("Rotate CCW");
  motor_A_control(HIGH,0);
  motor_B_control(LOW,0);

  //왼쪽으로 90도 회전 한다
  motor_A_control(LOW,95); 
  motor_B_control(HIGH,100); 
  delay(700);
  
  //정지 한다 
  motor_A_control(HIGH,0);
  motor_B_control(LOW,0);

  }

  else if(maze_status == 2)
  {
  //정지 한다
  Serial.println("Rotate CCW");
  motor_A_control(HIGH,0);
  motor_B_control(LOW,0);

  //오른쪽으로 90도 회전 한다
  motor_A_control(HIGH,90); 
  motor_B_control(LOW,95); 
  delay(700);
  //정지 한다
  motor_A_control(HIGH,0);
  motor_B_control(LOW,0);

  } 

  else
  {
   //앞으로 간다
  Serial.println("Go Straight");
  motor_A_control(HIGH,200);
  motor_B_control(HIGH,210);
  }

 }
